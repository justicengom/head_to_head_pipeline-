import os
from pathlib import Path
from typing import Union, Dict

import pandas as pd

GB = 1_024
PathLike = Union[str, Path, os.PathLike]


configfile: "config.yaml"


inclusion_expr = "failed_qc == 0 and excluded == 0"
samplesheet = pd.read_csv(config["samplesheet"]).query(inclusion_expr)
containers: Dict[str, PathLike] = config["containers"]
envs: Dict[str, PathLike] = config["envs"]
scripts: Dict[str, PathLike] = config["scripts"]
rule_log_dir = Path("logs/stderr").resolve()
baseline_dir = Path(config["baseline_dir"]).resolve()
pandora_dir = Path(config["pandora_dir"]).resolve()
qc_dir = Path(config["qc_dir"]).resolve()
plot_dir = Path("plots").resolve()
report_dir = Path("report").resolve()

output_files = set()
output_files.add("report/clustering.processed.ipynb")
output_files.add(report_dir / "mixed_simulations.summary.html")
output_files.add(plot_dir / "mixed_simulations.png")

# ======================================================
# Sub Workflows
# todo: the subworkflow system is not working with this pipeline for some reason
# ======================================================
# subworkflow pandora:
#     workdir: str(pandora_dir)
#
#
# subworkflow baseline:
#     workdir: str(baseline_dir)


# subworkflow qc:
#     workdir: str(qc_dir)


report: report_dir / "workflow.rst"


rule all:
    input:
        output_files,


rule clustering:
    input:
        illumina_dist=baseline_dir / "distance/compass.matrix.csv",
        ont_dist=baseline_dir / "distance/bcftools.matrix.csv",
    output:
        report_dir / "cluster_metrics.csv",
    conda:
        envs["clustering"]
    params:
        thresholds=config["bcftools_clustering_thresholds"],
        figsize=(10, 10),
        node_size=400,
    log:
        "report/clustering.processed.ipynb",
    notebook:
        "notebooks/clustering.py.ipynb"


rule mixed_simulations:
    input:
        compass_matrix=rules.clustering.input.illumina_dist,
        bcftools_matrix=rules.clustering.input.ont_dist,
        mixed_matrix=baseline_dir / "distance/mixed.matrix.csv",
    output:
        plot=report(
            plot_dir / "mixed_simulations.png",
            caption=report_dir / "mixed_simulations_plot.rst",
            category="Mixed Tech",
            subcategory="Simulations",
        ),
        summary=report(
            report_dir / "mixed_simulations.summary.html",
            caption=report_dir / "mixed_simulations_summary.rst",
            category="Mixed Tech",
            subcategory="Simulations",
        ),
    threads: 1
    resources:
        mem_mb=int(2 * GB),
    conda:
        envs["mixed_simulations"]
    params:
        seed=1988,
        thresholds={
            5: {"ont": 6, "mixed": 6},
            12: {"ont": 12, "mixed": 12},
        },
        ratios=[0.01, 0.05, 0.1, 0.25, 0.5, 0.75, 0.9],
        num_simulations=1000,
        figsize=(13, 13),
        dpi=300,
        legend_loc=(0.91, 0.53),
        nrows=2,
        ncols=1,
        sharex=True,
        sharey=False,
    log:
        rule_log_dir / "mixed_simulations.log",
    script:
        scripts["mixed_simulations"]
