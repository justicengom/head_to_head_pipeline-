import os
from pathlib import Path
from typing import Dict, Union

import pandas as pd
from snakemake.utils import min_version

min_version("5.14.0")

GB = 1_024
PathLike = Union[str, Path, os.PathLike]


# ======================================================
# Config files
# ======================================================
configfile: "config.yaml"


# only load samples that passed QC and are not excluded
inclusion_expr = "failed_qc == 0 and excluded == 0"
samplesheet = pd.read_csv(config["samplesheet"]).query(inclusion_expr)
containers: Dict[str, PathLike] = config["containers"]
envs: Dict[str, PathLike] = config["envs"]
scripts: Dict[str, PathLike] = config["scripts"]
rule_log_dir = Path("logs/stderr")
H37RV = config["h37rv"]
data_dir = Path(config["data_dir"])
filters = config["filters"]
report_dir = Path("report")
# plot_dir = report_dir / "plotting"
compass_vcf_dir = Path(config["compass_vcf_dir"])
asm_dir = Path(config["asm_dir"])
nanopore_dir = Path("nanopore")
concordance_dir = Path("concordance")
truth_eval_dir = Path("truth_eval")

# ======================================================
# Global functions and variables
# ======================================================
SITES = samplesheet["site"]
SAMPLES = samplesheet["sample"]
output_files = set()
output_files.add(concordance_dir / "alt_concordance.html")
FILTER_LOGS = set()
for idx, row in samplesheet.iterrows():
    site = row["site"]
    sample = row["sample"]
    FILTER_LOGS.add(rule_log_dir / f"filter_nanopore_snps/{site}/{sample}.log")

    if row["pacbio"] == 1:
        compass_truth_summary = truth_eval_dir / f"{sample}/compass/summary_stats.json"
        bcftools_truth_summary = (
            truth_eval_dir / f"{sample}/bcftools/summary_stats.json"
        )
        output_files.add(compass_truth_summary)
        output_files.add(bcftools_truth_summary)


# ======================================================
# Rules
# ======================================================
localrules:
    all,


report: report_dir / "workflow.rst"


rule all:
    input:
        output_files,


rule index_reference:
    input:
        H37RV["genome"],
    output:
        H37RV["genome"] + ".fai",
    log:
        rule_log_dir / "index_reference.log",
    container:
        containers["conda"]
    wrapper:
        "0.63.0/bio/samtools/faidx"


rule map_nanopore:
    input:
        target=H37RV["genome"],
        query=data_dir / "{site}/nanopore/{sample}/{sample}.subsampled.fastq.gz",
    output:
        sam=temp(nanopore_dir / "mapped/{site}/{sample}.sorted.sam"),
    threads: 8
    resources:
        mem_mb=lambda wildcards, attempt: int(8 * GB) * attempt,
    container:
        containers["conda"]
    conda:
        envs["aln_tools"]
    params:
        minimap_extras=" ".join(["-a", "-L", "--sam-hit-only", "--secondary=no"]),
        minimap_preset="map-ont",
    log:
        rule_log_dir / "map_nanopore/{site}/{sample}.log",
    shell:
        """
        (minimap2 {params.minimap_extras} \
            -x {params.minimap_preset} \
            -t {threads} \
            {input.target} {input.query} | \
        samtools sort -@ {threads} -o {output.sam}) 2> {log}
        """


rule pileup_nanopore:
    input:
        index=rules.index_reference.output,
        ref=rules.index_reference.input[0],
        alignments=rules.map_nanopore.output.sam,
    output:
        pileup=nanopore_dir / "pileups/{site}/{sample}.pileup.bcf",
    threads: 2
    resources:
        mem_mb=lambda wildcards, attempt: {
            1: int(64 * GB),
            2: int(80 * GB),
            3: int(100 * GB),
        }[attempt],
    params:
        options="--ignore-overlaps -O b",
    log:
        rule_log_dir / "pileup_nanopore/{site}/{sample}.log",
    container:
        containers["conda"]
    wrapper:
        "0.64.0/bio/bcftools/mpileup"


rule call_snps_nanopore:
    input:
        pileup=rules.pileup_nanopore.output.pileup,
    output:
        calls=nanopore_dir / "snp_calls/{site}/{sample}.snps.bcf",
    threads: 2
    resources:
        mem_mb=lambda wildcards, attempt: int(GB) * attempt,
    params:
        caller="-m",
        options=" ".join(["--ploidy 1", "-O b", "--skip-variants indels"]),
    log:
        rule_log_dir / "call_snps_nanopore/{site}/{sample}.log",
    container:
        containers["conda"]
    wrapper:
        "0.64.0/bio/bcftools/call"


rule filter_nanopore_snps:
    input:
        vcf=rules.call_snps_nanopore.output.calls,
    output:
        vcf=nanopore_dir / "filtered_snps/{site}/{sample}.snps.filtered.bcf",
    threads: 1
    resources:
        mem_mb=int(GB),
    params:
        min_depth=filters["min_depth"],
        max_depth=filters["max_depth"],
        min_qual=filters["min_qual"],
        min_strand_bias=filters["min_strand_bias"],
        options="--hist --verbose --overwrite",
        script=scripts["filter"],
    log:
        rule_log_dir / "filter_nanopore_snps/{site}/{sample}.log",
    container:
        containers["conda"]
    conda:
        envs["filter"]
    shell:
        """
        python {params.script} {params.options} \
            -i {input.vcf} \
            -o {output.vcf} \
            -d {params.min_depth} \
            -D {params.max_depth} \
            -s {params.min_strand_bias} \
            -q {params.min_qual} 2> {log}
        """


rule calculate_concordance:
    input:
        truth=compass_vcf_dir / "{sample}.compass.vcf.gz",
        query=rules.filter_nanopore_snps.output.vcf,
        mask=H37RV["mask"],
    output:
        csv=concordance_dir / "{site}/{sample}.concordance.csv",
        json=report(
            concordance_dir / "{site}/{sample}.concordance.json",
            category="Concordance",
            subcategory="Raw Data",
            caption=f"{report_dir}/calculate_concordance.rst",
        ),
    threads: 1
    resources:
        mem_mb=lambda wildcards, attempt: int(2 * GB) * attempt,
    params:
        options="--verbose --apply-filter",
        script=scripts["concordance"],
    log:
        rule_log_dir / "calculate_concordance/{site}/{sample}.log",
    container:
        containers["conda"]
    conda:
        envs["concordance"]
    shell:
        """
        python {params.script} {params.options} \
            -a {input.truth} \
            -b {input.query} \
            -m {input.mask} \
            -c {output.csv} \
            -j {output.json} 2> {log}
        """


rule plot_concordance:
    input:
        jsons=expand(
            f"{concordance_dir}/{{site}}/{{sample}}.concordance.json",
            zip,
            site=SITES,
            sample=SAMPLES,
        ),
        filter_logs=FILTER_LOGS,
    output:
        alt_plot=report(
            concordance_dir / "alt_concordance.html",
            caption=f"{report_dir}/alt_concordance.rst",
            category="Concordance",
            subcategory="Plots",
        ),
        gw_plot=report(
            concordance_dir / "gw_concordance.html",
            caption=f"{report_dir}/gw_concordance.rst",
            category="Concordance",
            subcategory="Plots",
        ),
        depth_call_rate_plot=report(
            concordance_dir / "depth_call_rate.html",
            caption=f"{report_dir}/depth_call_rate.rst",
            category="Concordance",
            subcategory="Plots",
        ),
        depth_gw_call_rate_plot=report(
            concordance_dir / "depth_gw_call_rate.html",
            caption=f"{report_dir}/depth_gw_call_rate.rst",
            category="Concordance",
            subcategory="Plots",
        ),
        depth_concordance_plot=report(
            concordance_dir / "depth_concordance.html",
            caption=f"{report_dir}/depth_concordance.rst",
            category="Concordance",
            subcategory="Plots",
        ),
        depth_gw_concordance_plot=report(
            concordance_dir / "depth_gw_concordance.html",
            caption=f"{report_dir}/depth_gw_concordance.rst",
            category="Concordance",
            subcategory="Plots",
        ),
    threads: 1
    resources:
        mem_mb=lambda wildcards, attempt: int(0.5 * GB) * attempt,
    params:
        colour_by="site",
        index="sample",
        log_scale=True,
    container:
        containers["conda"]
    conda:
        envs["plot_concordance"]
    script:
        scripts["plot_concordance"]


rule evaluate_compass_with_truth_assembly:
    input:
        truth_asm=asm_dir / "{sample}/flye/pacbio/decontam.assembly.flye.pacbio.fasta",
        vcf_ref=H37RV["genome"],
        vcf_to_eval=compass_vcf_dir / "{sample}.compass.vcf.gz",
        ref_mask=H37RV["mask"],
        truth_mask=(
            asm_dir
            / "{sample}/flye/pacbio/assessment/{sample}.flye.accuracy.pacbio.bed"
        ),
    output:
        summary=truth_eval_dir / "{sample}/compass/summary_stats.json",
    threads: 4
    resources:
        mem_mb=lambda wildcards, attempt: int(4 * GB) * attempt,
    params:
        options="--force",
        flank_length=100,
        outdir=lambda wildcards, output: Path(output.summary).parent,
    log:
        rule_log_dir / "evaluate_compass_with_truth_assembly/{sample}.log",
    container:
        containers["varifier"]
    shell:
        """
        varifier vcf_eval {params.options} \
            --flank_length {params.flank_length} \
            --ref_mask {input.ref_mask} \
            --truth_mask {input.truth_mask} \
            {input.truth_asm} \
            {input.vcf_ref} \
            {input.vcf_to_eval} \
            {params.outdir} 2> {log}
        """


rule evaluate_bcftools_with_truth_assembly:
    input:
        truth_asm=asm_dir / "{sample}/flye/pacbio/decontam.assembly.flye.pacbio.fasta",
        vcf_ref=H37RV["genome"],
        vcf_to_eval=(
            nanopore_dir / "filtered_snps/madagascar/{sample}.snps.filtered.bcf"
        ),
        ref_mask=H37RV["mask"],
        truth_mask=(
            asm_dir
            / "{sample}/flye/pacbio/assessment/{sample}.flye.accuracy.pacbio.bed"
        ),
    output:
        summary=truth_eval_dir / "{sample}/bcftools/summary_stats.json",
    threads: 4
    resources:
        mem_mb=lambda wildcards, attempt: int(4 * GB) * attempt,
    params:
        options="--force",
        flank_length=100,
        outdir=lambda wildcards, output: Path(output.summary).parent,
    log:
        rule_log_dir / "evaluate_bcftools_with_truth_assembly/{sample}.log",
    container:
        containers["conda"]
    conda:
        envs["varifier"]
    shell:
        """
        (bcftools view -O v {input.vcf_to_eval} \
        | varifier vcf_eval {params.options} \
            --flank_length {params.flank_length} \
            --ref_mask {input.ref_mask} \
            --truth_mask {input.truth_mask} \
            {input.truth_asm} \
            {input.vcf_ref} \
            - \
            {params.outdir}) 2> {log}
        """
