from itertools import product

import pandas as pd
from pathlib import Path


configfile: "config/config.yaml"


GB = 1_024
inclusion_expr = "failed_qc == 0 and excluded == 0"
samplesheet = pd.read_csv(config["samplesheet"]).query(inclusion_expr)
RESULTS = Path("results").resolve()
RESOURCES = Path("resources").resolve()
SCRIPTS = Path("workflow/scripts").resolve()
NOTEBOOKS = Path("workflow/notebooks").resolve()
ENVS = Path("workflow/envs").resolve()
RULES = Path("workflow/rules").resolve()
LOGS = Path("logs/rules").resolve()
CONTAINERS = config["containers"]
H37Rv = Path("../../../data/H37Rv_PRG/resources/h37rv.fa").resolve()
COMPASS_DIR = Path("../baseline_variants/illumina/gvcfs").resolve()

# generate required output files
TECHS = ["illumina", "nanopore"]
TOOLS = ["mykrobe", "drprg"]
OUTPUT = set()
for idx, row in samplesheet.iterrows():
    site = row["site"]
    sample = row["sample"]
    for tool, tech in product(TOOLS, TECHS):
        OUTPUT.add(
            RESULTS / f"novel/assessment/{tech}/{site}/{sample}/{sample}.summary.csv"
        )
        OUTPUT.add(RESULTS / f"concordance/{tool}/{tech}/{site}/{sample}.{tool}.csv")


OUTPUT.add(RESULTS / "drprg/popn_prg/prgs/dr.prg.k15.w14.idx")
OUTPUT.add(RESULTS / "figures/analysis.processed.ipynb")


subworkflow QC:
    workdir:
        "../../../data/QC"


subworkflow BuildPrg:
    workdir:
        "../../../data/H37Rv_PRG"


localrules:
    download_panel,


rule all:
    input:
        OUTPUT,


include: RULES / "common.smk"
include: RULES / "prg.smk"
include: RULES / "mykrobe.smk"
include: RULES / "concordance.smk"
include: RULES / "drprg.smk"
include: RULES / "novel.smk"
