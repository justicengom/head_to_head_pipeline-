from pathlib import Path
import pandas as pd
from snakemake.utils import min_version

min_version("7.14.1")


configfile: "config/config.yaml"


CONTAINERS = config["containers"]
DATA = Path(config["data_dir"])
ARCHIVE = Path(config["archive"])
SCRIPTS = Path("workflow/scripts").resolve()
ENVS = Path("workflow/envs").resolve()
LOGS = Path("logs/").resolve()
GB = 1_024

samplesheet = pd.read_csv(config["samplesheet"])
target_files = set()
blow5s = set()

for i, row in samplesheet.iterrows():
    site = row.site
    run = row.run
    blow5s.add(Path(ARCHIVE / f"nanopore/{site}.{run}.blow5"))

target_files.add(ARCHIVE / "nanopore/SHA256SUM")


rule all:
    input:
        target_files,


rule fast5_to_blow5:
    input:
        fast5_dir=DATA / "{site}/nanopore/raw_data/{run}/multi_fast5s",
    output:
        blow5=ARCHIVE / "nanopore/{site}.{run}.blow5",
    log:
        LOGS / "fast5_to_slow5/{site}/{run}.log",
    threads: 8
    resources:
        mem_mb=int(16 * GB),
    conda:
        ENVS / "fast5_to_blow5.yaml"
    script:
        SCRIPTS / "mixed-multi-fast5-to-blow5.sh"


rule checksum:
    input:
        blow5s=blow5s,
    output:
        checksum=ARCHIVE / "nanopore/SHA256SUM",
    log:
        LOGS / "checksum.log",
    container:
        CONTAINERS["base"]
    shell:
        "sha256sum {input.blow5s} > {output.checksum} 2> {log}"
