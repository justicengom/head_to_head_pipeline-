from pathlib import Path
import pandas as pd


configfile: "config/config.yaml"


CONTAINERS = config["containers"]
DATA = Path(config["data_dir"])
ARCHIVE = Path(config["archive"])
LOGS = Path("logs/").resolve()
GB = 1_024

samplesheet = pd.read_csv(config["samplesheet"])
target_files = set()
blow5s = set()

for i, row in samplesheet.iterrows():
    site = row.site
    run = row.run
    blow5s.add(Path(ARCHIVE / f"nanopore/{site}.{run}.blow5"))

target_files.add(ARCHIVE / "nanopore/SHA256SUM")


rule all:
    input:
        target_files,


rule fast5_to_blow5:
    input:
        fast5_dir=DATA / "{site}/nanopore/raw_data/{run}/multi_fast5s",
    output:
        outdir=temp(directory(ARCHIVE / "nanopore/blow5/{site}/{run}")),
    log:
        LOGS / "fast5_to_slow5/{site}/{run}.log",
    threads: 8
    resources:
        mem_mb=int(4 * GB),
    container:
        CONTAINERS["slow5"]
    params:
        opts="--to blow5",
    shell:
        "slow5tools f2s {params.opts} -p {threads} -d {output.outdir} {input.fast5_dir} 2> {log}"


rule merge_blow5:
    input:
        indir=rules.fast5_to_blow5.output.outdir,
    output:
        blow5=ARCHIVE / "nanopore/{site}.{run}.blow5",
    log:
        LOGS / "merge_blow5/{site}/{run}.log",
    threads: 8
    resources:
        mem_mb=int(4 * GB),
    container:
        CONTAINERS["slow5"]
    shell:
        "slow5tools merge -t {threads} -o {output.blow5} {input.indir} 2> {log}"


rule checksum:
    input:
        blow5s=blow5s,
    output:
        checksum=ARCHIVE / "nanopore/SHA256SUM",
    log:
        LOGS / "checksum.log",
    container:
        CONTAINERS["base"]
    shell:
        "sha256sum {input.blow5s} > {output.checksum} 2> {log}"
